buildscript {
    repositories {
        maven {
            url = 'http://files.minecraftforge.net/maven'
        }
		jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.3.0'
    id "maven-publish"
}

apply plugin: 'maven'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'signing'

version = "1.14.x-2.1.9a"

def buildID = Long.toUnsignedString(new Random().nextLong(), 16).toUpperCase()

def fileVersion = "2.1.9a"
def signSHA1 = "1945fa1cbb8ec2ffad2ccec7a0ca72ad52b20ef8"
group= "net.jomcraft.defaultsettings"
ext.isReleaseVersion = project.hasProperty("isSonaType") ? isSonaType : false
archivesBaseName = "DefaultSettings"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: '20190719-1.14.3'

	runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            source sourceSets.main
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            source sourceSets.main
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.14.4-28.2.3'
}

def forge_version = "1.14.4-28.2.3"

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        expand 'version': fileVersion, 'forge_version': forge_version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}

jar {

	println "BuildID: $buildID"

	exclude('net/**') 

    manifest {
        attributes([
            "Specification-Title": "DefaultSettings",
            "Specification-Vendor": "Jomcraft Network",
            "Specification-Version": fileVersion,
            "Implementation-Title": "DefaultSettings",
            "Implementation-Version": fileVersion,
            "Implementation-Vendor" : "Jomcraft Network",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "Build-ID": buildID
        ])
    }
}

task signJar {
    doLast {
        configurations.archives.allArtifacts.files.each {
            if(it.name.endsWith(".jar")){
            	ant.signjar(
                	jar: it,
					storetype:"jks",
					alias: project.property("signing.alias"),
                	storepass:project.property("signing.keyStorePassword"),
                	keystore: project.property("signing.keystorePath"),
                	keypass: project.property("signing.keyStorePassword"),
                	preservelastmodified:"true"
            	)
            	//if(!isReleaseVersion)
            	signing.sign(it)
				project.logger.lifecycle("JAR Signed: " + it.name)
			}
       }
    }
}

build.dependsOn signJar

uploadArchives.dependsOn signJar

signing {
	if(isReleaseVersion)
    	sign configurations.archives
}

subprojects {
    tasks.withType(Javadoc).all { enabled = isReleaseVersion }
}

task sourcesJar(type: Jar, dependsOn: classes) {

	exclude('net/**') 
	
	from sourceSets.main.allSource
	classifier = 'sources'
}

task deobfJar(type: Jar) {
	
	exclude('net/**')
	
	from sourceSets.main.output
	classifier = 'deobf'
}

task javadocJar(type: Jar) {

	exclude('net/**')
    classifier = 'javadoc'
    from javadoc
}

artifacts {
	archives sourcesJar
	archives deobfJar
	if(isReleaseVersion)
		archives javadocJar
}

task zipVerification {
    doLast {
        ant.zip(destfile: "$buildDir/libs/verification.zip") {
            fileset(dir: "$buildDir/libs/") {
                include(name: '*.asc')
            }
            fileset(dir: "") {
                include(name: 'pubkey.gpg')
            }
        }
        delete fileTree("$buildDir/libs/") {
			include '**/*.asc'
		}
        project.logger.lifecycle("Created verification ZIP")
    }
}

build.dependsOn zipVerification

curseforge {

  apiKey = project.property("cursegradle.api_token")
  project {
    id = '318012'
    changelog = file("changelog.html")
    changelogType = 'html'
    releaseType = 'release'
    addGameVersion '1.14.2'
    addGameVersion '1.14.3'
    addGameVersion '1.14.4'
    mainArtifact(jar) {
    	displayName = "$archivesBaseName-$version"
    }
    
    addArtifact (sourcesJar) {
    	releaseType = 'release'
    	displayName = "$archivesBaseName-$version-sources"
    }
    
    addArtifact (deobfJar) {
    	releaseType = 'release'
    	displayName = "$archivesBaseName-$version-deobf"
    }
    
    addArtifact ("$buildDir/libs/verification.zip") {
    	releaseType = 'release'
    	displayName = 'GPGVerification'
    }
    
  }

}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment mvnd ->
               signing.signPom(mvnd)
	  }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'DefaultSettings'
        packaging 'jar'
        artifactId 'DefaultSettings-1.14.x'
        version fileVersion
        description 'DefaultSettings Minecraft Mod API'
        url 'https://gitlab.com/jomcraft-sources/defaultsettings'

        scm {
        	connection 'scm:git:git://gitlab.com/jomcraft-sources/defaultsettings.git'
        	developerConnection 'scm:git:git@gitlab.com:jomcraft-sources/defaultsettings.git'
        	url 'https://gitlab.com/jomcraft-sources/defaultsettings'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'PT400C'
            name 'Rolf Marker'
            email 'pt400c@gmail.com'
          }
        }
      }
    }
  }
}