buildscript {
    repositories {
        maven {
            url = 'http://files.minecraftforge.net/maven'
        }
		jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.3.0'
    id "maven-publish"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'signing'

version = "1.13.2-1.3.0"


def fileVersion = "1.3.0"
def signSHA1 = "1945fa1cbb8ec2ffad2ccec7a0ca72ad52b20ef8"
group= "de.pt400c.defaultsettings"

def keystoreFile = new File(project.gradle.gradleUserHomeDir.absolutePath + '/keystore.jks')

ext."signing.secretKeyRingFile" = new File(project.gradle.gradleUserHomeDir.absolutePath + '/keyring.gpg')

archivesBaseName = "DefaultSettings"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: '20180921-1.13'

	runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            source sourceSets.main
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            source sourceSets.main
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.13.2-25.0.219'
}

def minecraft_version = "1.13.2"

def forge_version = "25.0.219"

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        expand 'version': fileVersion, 'mcversion': minecraft_version, 'forge_version': forge_version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}

jar {
    manifest {
        attributes([
            "Specification-Title": "DefaultSettings",
            "Specification-Vendor": "Jomcraft Network",
            "Specification-Version": fileVersion,
            "Implementation-Title": "DefaultSettings",
            "Implementation-Version": fileVersion,
            "Implementation-Vendor" : "Jomcraft Network",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task signJar {

    doLast {
        configurations.archives.allArtifacts.files.each {
            ant.signjar(
                jar: it,
				storetype:"jks",
				alias: project.property("signing.alias"),
                storepass: project.property("signing.keyStorePassword"),
                keystore: "${keystoreFile.absolutePath}",
                keypass: project.property("signing.keyStorePassword"),
                preservelastmodified:"true"
            )
            signing.sign(it)
			project.logger.lifecycle("JAR Signed: " + it.name)
        }
    }
}

build.dependsOn signJar

task sourcesJar(type: Jar, dependsOn: classes) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

task deobfJar(type: Jar) {
	from sourceSets.main.output
	classifier = 'deobf'
}

artifacts {
	archives sourcesJar
	archives deobfJar
}

task zipVerification {
    doLast {
        ant.zip(destfile: "$buildDir/libs/verification.zip") {
            fileset(dir: "$buildDir/libs/") {
                include(name: '*.asc')
            }
            fileset(dir: "") {
                include(name: 'pubkey.gpg')
            }
        }
        project.logger.lifecycle("Created verification ZIP")
    }
}

build.dependsOn zipVerification

curseforge {

  apiKey = project.property("cursegradle.api_token")
  project {
    id = '318012'
    releaseType = 'release'
    mainArtifact(jar) {
    	displayName = "$archivesBaseName-$version"
    }
    
    addArtifact (sourcesJar) {
    	displayName = "$archivesBaseName-$version-sources"
    }
    
    addArtifact (deobfJar) {
    	displayName = "$archivesBaseName-$version-deobf"
    }
    
    addArtifact ("$buildDir/libs/verification.zip") {
    	releaseType = 'release'
    	displayName = 'GPGVerification'
    }
    
  }

}

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
        	groupId = 'de.pt400c.defaultsettings'
            artifactId = 'DefaultSettings-1.13.2'
            version = "$fileVersion"
            artifact jar
            artifact deobfJar
            artifact sourcesJar
           
            artifact("$buildDir/libs/verification.zip"){
           		classifier "verification"
        		extension "zip"
            }
            
        }
    }
  repositories {
  		maven {
  			credentials {
        		username project.property("maven.username")
        		password project.property("maven.password")
    		}
    	url "https://maven.jomcraft.net/repository/release"
    }
    
}
}
