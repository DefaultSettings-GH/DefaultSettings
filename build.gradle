buildscript {
    repositories {
        maven {
            url = 'http://files.minecraftforge.net/maven'
        }
		jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "maven-publish"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'signing'

version = "1.15.2-2.1.9"

def buildID = Long.toUnsignedString(new Random().nextLong(), 16).toUpperCase()

def fileVersion = "2.1.9"
def signSHA1 = "1945fa1cbb8ec2ffad2ccec7a0ca72ad52b20ef8"
group= "de.pt400c.defaultsettings"

archivesBaseName = "DefaultSettings"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: '20200225-1.15.1'

	runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            source sourceSets.main
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            source sourceSets.main
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.15.2-31.1.24'
}

def forge_version = "31.1.24"

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        expand 'version': fileVersion, 'forge_version': forge_version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}

jar {

	println "BuildID: $buildID"

	exclude('net/**') 

    manifest {
        attributes([
            "Specification-Title": "DefaultSettings",
            "Specification-Vendor": "Jomcraft Network",
            "Specification-Version": fileVersion,
            "Implementation-Title": "DefaultSettings",
            "Implementation-Version": fileVersion,
            "Implementation-Vendor" : "Jomcraft Network",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "Build-ID": buildID
        ])
    }
}

task signJar {
    doLast {
        configurations.archives.allArtifacts.files.each {
            ant.signjar(
                jar: it,
				storetype:"jks",
				alias: project.property("signing.alias"),
                storepass:project.property("signing.keyStorePassword"),
                keystore: project.property("signing.keystorePath"),
                keypass: project.property("signing.keyStorePassword"),
                preservelastmodified:"true"
            )
            signing.sign(it)
			project.logger.lifecycle("JAR Signed: " + it.name)
        }
    }
}

build.dependsOn signJar

task sourcesJar(type: Jar, dependsOn: classes) {

	exclude('net/**') 
	
	from sourceSets.main.allSource
	classifier = 'sources'
}

task deobfJar(type: Jar) {
	
	exclude('net/**')
	
	from sourceSets.main.output
	classifier = 'deobf'
}

artifacts {
	archives sourcesJar
	archives deobfJar
}


task zipVerification {
    doLast {
        ant.zip(destfile: "$buildDir/libs/verification.zip") {
            fileset(dir: "$buildDir/libs/") {
                include(name: '*.asc')
            }
            fileset(dir: "") {
                include(name: 'pubkey.gpg')
            }
        }
        delete fileTree("$buildDir/libs/") {
			include '**/*.asc'
		}
        project.logger.lifecycle("Created verification ZIP")
    }
}

build.dependsOn zipVerification

curseforge {

  apiKey = project.property("cursegradle.api_token")
  project {
    id = '318012'
    changelog = file("changelog.html")
    changelogType = 'html'
    releaseType = 'release'
    addGameVersion '1.15.2'
    addGameVersion '1.15.1'
    addGameVersion '1.15'
    mainArtifact(jar) {
    	displayName = "$archivesBaseName-$version"
    }
    
    addArtifact (sourcesJar) {
    	releaseType = 'release'
    	displayName = "$archivesBaseName-$version-sources"
    }
    
    addArtifact (deobfJar) {
    	releaseType = 'release'
    	displayName = "$archivesBaseName-$version-deobf"
    }
    
    addArtifact ("$buildDir/libs/verification.zip") {
    	releaseType = 'release'
    	displayName = 'GPGVerification'
    }
    
  }

}

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
        	groupId = 'de.pt400c.defaultsettings'
            artifactId = 'DefaultSettings-1.15.2'
            version = "$fileVersion"
            artifact jar
            artifact deobfJar
            artifact sourcesJar
           
            artifact("$buildDir/libs/verification.zip"){
           		classifier "verification"
        		extension "zip"
            }
            
        }
    }
  repositories {
  		maven {
  			credentials {
        		username project.property("maven.username")
        		password project.property("maven.password")
    		}
    	url "https://maven.jomcraft.net/repository/release"
    }
    
}
}