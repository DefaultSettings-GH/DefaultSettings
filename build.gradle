buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.3.0'
    id "maven-publish"
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'
apply plugin: 'signing'

version = "1.8.x-1.12.x-2.2.0"
def buildID = Long.toUnsignedString(new Random().nextLong(), 16).toUpperCase()

def fileVersion = "2.2.0"
def signSHA1 = "1945fa1cbb8ec2ffad2ccec7a0ca72ad52b20ef8"
group = "net.jomcraft.defaultsettings"

ext.isReleaseVersion = project.hasProperty("isSonaType") ? isSonaType : false

archivesBaseName = "DefaultSettings"

minecraft {
    version = "1.12.2-14.23.5.2824"
	mappings = "snapshot_20171003"
    runDir = "eclipse"
    
    replaceIn 	"de/pt400c/defaultsettings/DefaultSettings.java"
	replace 	"@FINGERPRINT@", signSHA1
    replace   	"DS-Version", fileVersion
}

jar {
    println "BuildID: $buildID"
	exclude('net/**')   
    manifest {
        attributes("FMLAT": "defaultsettings_at.cfg")
		attributes 'FMLCorePlugin': 'de.pt400c.defaultsettings.core.DefaultSettingsPlugin'
        attributes 'FMLCorePluginContainsFMLMod': 'false'
        attributes([
            "Build-ID": buildID,
            "Build-Date": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
        
    }
}

processResources
{

    inputs.property "version", fileVersion
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':fileVersion, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task signJar {
    doLast {
        configurations.archives.allArtifacts.files.each {
           if(it.name.endsWith(".jar")){
            	ant.signjar(
                	jar: it,
					storetype:"jks",
					alias: project.property("signing.alias"),
                	storepass:project.property("signing.keyStorePassword"),
                	keystore: project.property("signing.keystorePath"),
                	keypass: project.property("signing.keyStorePassword"),
                	preservelastmodified:"true"
            	)
            	signing.sign(it)
				project.logger.lifecycle("JAR Signed: " + it.name)
			}
       }
    }
}

build.dependsOn signJar

uploadArchives.dependsOn signJar

signing {
	if(isReleaseVersion)
    	sign configurations.archives
}

subprojects {
    tasks.withType(Javadoc).all { enabled = isReleaseVersion }
}

task sourcesJar(type: Jar, dependsOn: classes) {

	exclude('net/**') 

	from sourceSets.main.allSource
	classifier = 'sources'
}

task deobfJar(type: Jar) {
	
	exclude('net/**') 

	from sourceSets.main.output
	classifier = 'deobf'
}


task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
	archives sourcesJar
	archives deobfJar
	if(isReleaseVersion)
		archives javadocJar
}

task zipVerification {
    doLast {
        ant.zip(destfile: "$buildDir/libs/verification.zip") {
            fileset(dir: "$buildDir/libs/") {
                include(name: '*.asc')
            }
            fileset(dir: "") {
                include(name: 'pubkey.gpg')
            }
        }
        if(!isReleaseVersion){
        	delete fileTree("$buildDir/libs/") {
				include '**/*.asc'
			}
		}
        project.logger.lifecycle("Created verification ZIP")
    }
}

build.dependsOn zipVerification

curseforge {
  apiKey = project.property("cursegradle.api_token")
  project {
    id = '318012'
    changelog = file("changelog.html")
    changelogType = 'html'
    releaseType = 'release'
    addGameVersion '1.8'
    addGameVersion '1.8.8'
    addGameVersion '1.8.9'
    addGameVersion '1.9'
    addGameVersion '1.9.4'
    addGameVersion '1.10'
    addGameVersion '1.10.2'
    addGameVersion '1.11'
    addGameVersion '1.11.2'
    addGameVersion '1.12'
    addGameVersion '1.12.1'
    addGameVersion '1.12.2'
    mainArtifact(jar) {
    	displayName = "$archivesBaseName-$version"
    }
    
    addArtifact (sourcesJar) {
		releaseType = 'release'
    	displayName = "$archivesBaseName-$version-sources"
    }
    
    addArtifact (deobfJar) {
		releaseType = 'release'
    	displayName = "$archivesBaseName-$version-deobf"
    }
    
    addArtifact ("$buildDir/libs/verification.zip") {
    	releaseType = 'release'
    	displayName = 'GPGVerification'
    }
    
  }
  
  }
  
  uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment mvnd ->
               signing.signPom(mvnd)
	  }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'DefaultSettings'
        packaging 'jar'
		artifactId 'DefaultSettings-1.8.x-1.12.x'
        version fileVersion
        description 'https://github.com/DefaultSettings-GH/'
        url 'https://github.com/DefaultSettings-GH/DefaultSettings-GH'

        scm {
        	connection 'https://github.com/DefaultSettings-GH/DefaultSettings-GH.git'
        	developerConnection 'https://github.com/DefaultSettings-GH/DefaultSettings-GH.git'
        	url 'https://github.com/DefaultSettings-GH/DefaultSettings-GH'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'PT400C'
            name 'Rolf Marker'
            email 'pt400c@gmail.com'
          }
        }
      }
    
  }
}
}